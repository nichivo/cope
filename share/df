#!/usr/bin/env perl
use App::Cope;
use App::Cope::Extra qw[percent_b];

my @headers = (
  'Filesystem', '1K-blocks', 'Used', qr/Avail(?:able)?/, 'Use%', 'Mounted on',
  'Size', 'Type', # -T
  'Inodes', 'IUsed', 'IFree', 'IUse%', # -i
);

my $system_fs = join '|', qw[none tmpfs devtmpfs dev udev proc devpts sysfs procbususb rpc_pipefs run unionfs];

sub process {
  if (/^Filesystem/) {
    for my $h (@headers) { mark $h => 'underline'; }
  }
  else {
    line qr{(\d+%)\s+(\S+)$} => \&{ percent_b 25, 50, 75 }, 'bold';

    # filesystems
    mark qr{^(?:$system_fs)\b} => 'magenta'
      or line qr{(?:^|:|;)([^\s;]+/)([^\s;]+)} => 'blue', 'blue bold' # also matches multiple fuse devs: /mnt/d1;/mnt/d2;/mnt/d3 ...
      or line qr{(\S+:)(\S*)} => 'blue', 'blue bold'
      or line qr{^(automount\()(\S+)(\))} => 'red', 'magenta', 'red';

    line qr{^(?:$system_fs)\b} or line qr{(?:\s+)(\S+/)(\S+)$} => 'cyan', 'cyan bold';
  }
}

run( \&process, real_path, @ARGV );
